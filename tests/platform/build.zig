const std = @import("std");
const builtin = @import("builtin");

/// Roc target definitions matching src/cli/target.zig
const RocTarget = enum {
    // x64 (x86_64) targets
    x64mac,
    x64win,
    x64musl,
    x64v1musl,

    // arm64 (aarch64) targets
    arm64mac,
    arm64win,
    arm64musl,
    arm64v1musl,

    fn toZigTarget(self: RocTarget) std.Target.Query {
        return switch (self) {
            .x64mac => .{ .cpu_arch = .x86_64, .os_tag = .macos },
            .x64win => .{ .cpu_arch = .x86_64, .os_tag = .windows, .abi = .msvc },
            .x64musl => .{ .cpu_arch = .x86_64, .os_tag = .linux, .abi = .musl },
            .x64v1musl => .{
                .cpu_arch = .x86_64,
                .cpu_model = .{ .explicit = &std.Target.x86.cpu.x86_64 },
                .os_tag = .linux,
                .abi = .musl,
            },
            .arm64mac => .{ .cpu_arch = .aarch64, .os_tag = .macos },
            .arm64win => .{ .cpu_arch = .aarch64, .os_tag = .windows, .abi = .msvc },
            .arm64musl => .{ .cpu_arch = .aarch64, .os_tag = .linux, .abi = .musl },
            .arm64v1musl => .{
                .cpu_arch = .aarch64,
                .cpu_model = .{ .explicit = &std.Target.aarch64.cpu.generic },
                .os_tag = .linux,
                .abi = .musl,
            },
        };
    }

    fn targetDir(self: RocTarget) []const u8 {
        return switch (self) {
            .x64mac => "x64mac",
            .x64win => "x64win",
            .x64musl => "x64musl",
            .x64v1musl => "x64v1musl",
            .arm64mac => "arm64mac",
            .arm64win => "arm64win",
            .arm64musl => "arm64musl",
            .arm64v1musl => "arm64v1musl",
        };
    }

    fn libFilename(self: RocTarget) []const u8 {
        return switch (self) {
            .x64win, .arm64win => "host.lib",
            else => "libhost.a",
        };
    }

    fn baselineMuslTarget(self: RocTarget) ?RocTarget {
        return switch (self) {
            .x64musl => .x64v1musl,
            .arm64musl => .arm64v1musl,
            else => null,
        };
    }

    fn muslRuntimeSourceTarget(self: RocTarget) ?RocTarget {
        return switch (self) {
            .x64v1musl => .x64musl,
            .arm64v1musl => .arm64musl,
            else => null,
        };
    }
};

/// All cross-compilation targets for `zig build`
const all_targets = [_]RocTarget{
    .x64mac,
    .x64win,
    .x64musl,
    .x64v1musl,
    .arm64mac,
    .arm64win,
    .arm64musl,
    .arm64v1musl,
};

pub fn build(b: *std.Build) void {
    const optimize = b.standardOptimizeOption(.{});

    // Cleanup step: remove only generated host library files (preserve libc.a, crt1.o, etc.)
    const cleanup_step = b.step("clean", "Remove all built library files");
    for (all_targets) |roc_target| {
        cleanup_step.dependOn(&CleanupStep.create(b, b.path(
            b.pathJoin(&.{ "targets", roc_target.targetDir(), roc_target.libFilename() }),
        )).step);
    }

    // Default step: build for all targets (with cleanup first)
    const all_step = b.getInstallStep();
    all_step.dependOn(cleanup_step);

    // Create copy step for all targets
    const copy_all = b.addUpdateSourceFiles();
    all_step.dependOn(&copy_all.step);

    // Build for each Roc target
    for (all_targets) |roc_target| {
        const target = b.resolveTargetQuery(roc_target.toZigTarget());
        const host_lib = buildHostLib(b, target, optimize);

        // Copy to platform/targets/{target}/libhost.a (or host.lib for Windows)
        copy_all.addCopyFileToSource(
            host_lib.getEmittedBin(),
            b.pathJoin(&.{ "targets", roc_target.targetDir(), roc_target.libFilename() }),
        );

        if (roc_target.muslRuntimeSourceTarget()) |runtime_source_target| {
            for ([2][]const u8{ "crt1.o", "libc.a" }) |filename| {
                copy_all.addCopyFileToSource(
                    b.path(b.pathJoin(&.{ "targets", runtime_source_target.targetDir(), filename })),
                    b.pathJoin(&.{ "targets", roc_target.targetDir(), filename }),
                );
            }
        }
    }

    // Native step: build only for the current platform (with full cleanup first)
    const native_step = b.step("native", "Build host library for native platform only");
    native_step.dependOn(cleanup_step);

    const native_target = b.standardTargetOptions(.{});

    // Detect native RocTarget and copy to proper targets folder
    const native_roc_target = detectNativeRocTarget(native_target.result) orelse {
        std.debug.print("Unsupported native platform\n", .{});
        return;
    };

    const native_lib = buildHostLib(b, b.resolveTargetQuery(native_roc_target.toZigTarget()), optimize);
    b.installArtifact(native_lib);

    const copy_native = b.addUpdateSourceFiles();
    copy_native.addCopyFileToSource(
        native_lib.getEmittedBin(),
        b.pathJoin(&.{ "targets", native_roc_target.targetDir(), native_roc_target.libFilename() }),
    );

    if (native_roc_target.baselineMuslTarget()) |baseline_roc_target| {
        const baseline_lib = buildHostLib(b, b.resolveTargetQuery(baseline_roc_target.toZigTarget()), optimize);
        copy_native.addCopyFileToSource(
            baseline_lib.getEmittedBin(),
            b.pathJoin(&.{ "targets", baseline_roc_target.targetDir(), baseline_roc_target.libFilename() }),
        );
        for ([2][]const u8{ "crt1.o", "libc.a" }) |filename| {
            copy_native.addCopyFileToSource(
                b.path(b.pathJoin(&.{ "targets", native_roc_target.targetDir(), filename })),
                b.pathJoin(&.{ "targets", baseline_roc_target.targetDir(), filename }),
            );
        }
        native_step.dependOn(&baseline_lib.step);
    }
    native_step.dependOn(&copy_native.step);
    native_step.dependOn(&native_lib.step);

}

/// Detect which RocTarget matches the native platform
fn detectNativeRocTarget(target: std.Target) ?RocTarget {
    return switch (target.os.tag) {
        .macos => switch (target.cpu.arch) {
            .x86_64 => .x64mac,
            .aarch64 => .arm64mac,
            else => null,
        },
        .linux => switch (target.cpu.arch) {
            .x86_64 => .x64musl,
            .aarch64 => .arm64musl,
            else => null,
        },
        .windows => switch (target.cpu.arch) {
            .x86_64 => .x64win,
            .aarch64 => .arm64win,
            else => null,
        },
        else => null,
    };
}

/// Custom step to remove a single file if it exists
const CleanupStep = struct {
    step: std.Build.Step,
    path: std.Build.LazyPath,

    fn create(b: *std.Build, path: std.Build.LazyPath) *CleanupStep {
        const self = b.allocator.create(CleanupStep) catch @panic("OOM");
        self.* = .{
            .step = std.Build.Step.init(.{
                .id = .custom,
                .name = "cleanup",
                .owner = b,
                .makeFn = make,
            }),
            .path = path,
        };
        return self;
    }

    fn make(step: *std.Build.Step, options: std.Build.Step.MakeOptions) !void {
        _ = options;
        const self: *CleanupStep = @fieldParentPtr("step", step);
        const path = self.path.getPath2(step.owner, null);
        std.Io.Dir.cwd().deleteFile(step.owner.graph.io, path) catch |err| switch (err) {
            error.FileNotFound => {}, // Already gone, that's fine
            else => return err,
        };
    }
};

fn buildHostLib(
    b: *std.Build,
    target: std.Build.ResolvedTarget,
    optimize: std.builtin.OptimizeMode,
) *std.Build.Step.Compile {
    const host_lib = b.addLibrary(.{
        .name = "host",
        .linkage = .static,
        .root_module = b.createModule(.{
            .root_source_file = b.path("src/host.zig"),
            .target = target,
            .optimize = optimize,
            .strip = optimize != .Debug,
            .pic = true,
        }),
    });
    // Force bundle compiler-rt to resolve runtime symbols like __main
    host_lib.bundle_compiler_rt = true;

    return host_lib;
}
